plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.schinizer'
version = rxunfurlVersion

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

sourceSets.test {
    output.resourcesDir = output.classesDir
}

dependencies {
    compile "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"

    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile "com.squareup.okio:okio:${okioVersion}"

    compile "org.jsoup:jsoup:${jsoupVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller.pom.project {
        name 'RxUnfurl'
        description 'RxUnfurl'
        url 'https://github.com/Schinizer/RxUnfurl'

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        scm {
            url 'https://github.com/Schinizer/RxUnfurl'
            connection 'scm:git:https://github.com/Schinizer/RxUnfurl'
            developerConnection 'scm:git:https://github.com/Schinizer/RxUnfurl'
        }

        developers {
            developer {
                id 'schinizer'
                name 'Schinizer'
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'maven'
        name = 'RxUnfurl'
        userOrg = user
        desc = 'A reactive extension to generate URL previews.'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Schinizer/RxUnfurl.git'
        websiteUrl = 'https://github.com/Schinizer/RxUnfurl/'
        issueTrackerUrl = 'https://github.com/Schinizer/RxUnfurl/issues'

        githubRepo = 'Schinizer/RxUnfurl'
        githubReleaseNotesFile = 'README.md'

        version {
            name = rxunfurlVersion
            released  = new Date()
            vcsTag = rxunfurlVersion
        }
    }
}